buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE'
        classpath 'org.springframework:springloaded:1.2.6.RELEASE'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.13.0'
}

group 'com.github.zdix'
version '1.0'


apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
// apply plugin: 'war'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

jar {
    baseName = 'app'
    version =  ''
}

bootRepackage.withJarTask = jar

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    providedRuntime
}

dependencies {
    compile 'mysql:mysql-connector-java:5.1.40'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.apache.tomcat:tomcat-jdbc:8.5.5'
    compile 'com.google.guava:guava:19.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'redis.clients:jedis:2.9.0'

    compile group: 'org.mybatis.generator', name: 'mybatis-generator', version: '1.3.5', ext: 'pom'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-email', version: '1.4'


    compile 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.1.1'
    compile 'org.springframework.data:spring-data-redis:1.7.4.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:1.4.1.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.4.1.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-freemarker:1.4.1.RELEASE'

    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:1.4.1.RELEASE")
}

task publishBaseJar(type: Jar) {
    // classifier = 'base'
    from sourceSets.main.output
    exclude 'com/dix/app/**'
    exclude 'web/**'
    exclude 'application.properties'
    exclude 'mybatis/**'
    exclude 'mybatis-config.xml'
    exclude 'hibernate.cfg.xml'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // from components.java

            groupId 'com.dix.base'
            artifactId 'core'
            version '0.0.1'

            artifact publishBaseJar

            pom.withXml {
                def root = asNode();
                root.remove(root["dependencyManagement"])
                def dependenciesNode = root.appendNode('dependencies')
                // def dependenciesNode = asNode().getAt("dependencyManagement")[0].getAt("dependencies")[0]
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}